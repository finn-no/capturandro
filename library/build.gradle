description = 'Capturandro'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

dependencies {
    implementation('io.reactivex.rxjava2:rxjava:2.1.3')
    implementation('io.reactivex.rxjava2:rxandroid:2.0.1')
    implementation('androidx.legacy:legacy-support-v13:1.0.0')
    implementation('androidx.exifinterface:exifinterface:1.2.0')
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
    }

    packagingOptions {
        exclude 'AndroidManifest.xml'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        Publication(MavenPublication) {
            groupId project.group
            artifactId 'capturandro'
            version project.version
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/capturandro-release.aar")
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['Publication']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'capturandro'
        desc = 'Capturandro is an easy-to-use image import library for Android.'
        websiteUrl = 'https://github.com/finn-no/capturandro'
        issueTrackerUrl = 'https://github.com/finn-no/capturandro/issues'
        vcsUrl = 'https://github.com/finn-no/capturandro.git'
        licenses = ['Apache-2.0']
        labels = ['image','capture', 'photo', 'android','library','camera', 'rx']
        publicDownloadNumbers = true
        version {
            name = project.version
            mavenCentralSync {
                sync = false
            }
        }
    }
}